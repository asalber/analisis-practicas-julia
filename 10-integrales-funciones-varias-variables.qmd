---
title: Integrales de funciones de varias variables
lang: es
#jupyter: julia-1.9
---

## Ejercicios Resueltos

Para la realización de esta práctica se requieren los siguientes paquetes:

```{julia}
using SymPy  # Para el cálculo simbólico.
using HCubature # Para el cálculo numérico de integrales múltiples.
```

:::{#exr-volumen-figuras-geometricas}
Calcular el volumen de las siguientes figuras geométricas usando integrales múltiples.

:::{.callout-note collapse="true"}
## Ayuda
Para calcular numéricamente integrales definidas de funciones de varias variables usar la función [`hcubature`](https://juliapackages.com/p/hcubature) del paquete `HCubature`.
:::

a.  Un paralelogramo de base rectangular $[0,1]\times [0,5]$ y altura $10$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using HCubature
    f(x, y) = 10
    f(v) = f(v...)
    hcubature(f, [0,0], [1,5])
    ```
    :::

a.  Una cuña de base rectangular $[0,2]\times [0,5]$ y altura $10$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    f(x, y) = 10x
    f(v) = f(v...)
    hcubature(f, [0,0], [1,5])
    ```
    :::

a.  Un cilindro de base circular centrada en el origen con radio $1$ y altura $10$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    f(x, y) = x^2+y^2<1 ? 10 : 0
    f(v) = f(v...)
    hcubature(f, [-1,-1], [1,1])
    ```
    :::

a.  Una semiesfera centrada en el origen con radio $1$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    f(x, y) = x^2+y^2<1 ? sqrt(1-x^2-y^2) : 0
    f(v) = f(v...)
    hcubature(f, [-1,-1], [1,1])
    ```
    :::
:::

:::{#exr-integrales-iteradas}
Calcular las siguientes integrales iteradas.

:::{.callout-note collapse="true"}
## Ayuda
Para calcular la primitiva de una función se puede usar la función [`integrate`](https://docs.juliahub.com/SymPy/KzewI/1.0.31/Tutorial/calculus/#Integrals-1) del paquete [`SymPy`](https://docs.juliahub.com/SymPy/KzewI/1.0.31/).
:::

a.  $\int_0^1\int_0^2 x+y \,dy\,dx$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using SymPy
    @syms x y
    f(x, y) = x + y
    integrate(f(x,y), (y, 0, 2), (x, 0, 1))
    ```
    :::

a.  $\int_0^2\int_0^1 x+y \,dx\,dy$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    integrate(f(x,y), (x, 0, 1), (y, 0, 2))
    ```
    :::

a.  $\int_0^1\int_0^2\int_0^3 xyz \,dz\,dy\,dx$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    @syms z
    f(x, y, z) = x * y * z
    integrate(f(x,y,z), (z, 0, 3), (y, 0, 2), (x, 0, 1))
    ```
    :::

a.  $\int_0^3\int_0^1\int_0^2 xyz \,dy\,dx\,dz$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    @syms z
    f(x, y, z) = x * y * z
    integrate(f(x,y,z), (y, 0, 2), (x, 0, 1), (z, 0, 3))
    ```
    :::
:::

:::{#exr-integrales-dobles-regiones-irregulares}
Calcular las siguientes integrales dobles sobre las regiones dadas.

a.  $\int_0^2\int_0^{x/2} e^{x+y}\,dy\,dx$.

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    using SymPy
    @syms x y
    f(x, y) = exp(x+y)
    integrate(f(x,y), (y, 0, x/2), (x, 0, 2))
    ```
    :::

a.  Calcular la integral anterior invirtiendo el orden de integración.

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    integrate(f(x,y), (x, 2y, 2), (y, 0, 1))
    ```
    :::

a.  $\int_0^1\int_x^{\sqrt{x}} x^2y\,dy\,dx$.

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    f(x, y) = x^2*y
    integrate(f(x,y), (y, x, sqrt(x)), (x, 0, 1))
    ```
    :::

a.  Calcular la integral anterior invirtiendo el orden de integración.

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    integrate(f(x,y), (x, y^2, y), (y, 0, 1))
    ```
    :::

a.  $\int_0^1\int_y^1 \cos(x^2) \,dx\,dy$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    f(x, y) = cos(x^2)
    integrate(f(x,y), (x, y, 1), (y, 0, 1))
    ```

    Como la función $\cos(x^2)$ no tiene primitiva inmediata, el resultado aparece en función de a función de la [integral de Fresnel](https://es.wikipedia.org/wiki/Integral_de_Fresnel). Sin embargo, resulta más sencillo calcular esta integral doble iterada invirtiendo el orden de integración, es decir, $\int_0^1\int_0^x \cos(x^2) \,dy\,dx$.

    ```{julia}
    integrate(f(x,y), (y, 0, x), (x, 0, 1))
    ```
    :::
:::

:::{#exr-integrales-dobles-volumenes}
Calcular los volúmenes que quedan por debajo de las funciones siguientes en las regiones dadas.

a.  $x + 2y + 3z = 6$ en el primer octante.

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    using SymPy
    @syms x y z
    f(x, y) = solve(x+2y+3z-6, z)[1]
    g(x) = solve(f(x,y), y)
    integrate(f(x,y), (y, 0, g(x)), (x, 0, 6))
    ```
    :::

a.  $f(x,y) = xy$ en $[-1,1]\times [-1,1]$.

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    using HCubature
    f(x, y) = abs(x*y)
    f(v) = f(v...)
    hcubature(f, [-1,-1], [1,1])
    ```
    :::
:::

:::{#exr-volumen-balsa}
Un balsa de residuos líquidos con forma elíptica de ecuación $2x^2 + y^2 = 9$ tiene una profundidad dada por la función $f(x,y) = \frac{x^2}{2} + \frac{y^2}{2} - 10$. Calcular el volumen de la balsa.

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using SymPy
@syms x y
# Función de la superficie
f(x, y) = x^2/2 + y^2/2 - 10 
# Función de la región de integración
g(x, y) = 2x^2 + y^2 - 9
# Límites de integración en y
soly = solve(g(x, y), y)
# Límites de integración en x
solx = solve(g(x, 0))
integrate(f(x, y), (y, soly[1], soly[2]), (x, solx[1], solx[2]))
```
:::
:::

:::{#exr-volumen-entre-superficies}
Calcular el volumen comprendido entre las superficies de las funciones $f(x,y)=x^2+y^2$ y $g(x,y)=2x$.

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using SymPy
@syms x y
f(x, y) = x^2 + y^2
g(x, y) = 2x
sol = solve(f(x,y)-g(x,y), y)
integrate(g(x,y)-f(x,y), (y, sol[1], sol[2]), (x, 0, 2))
```
:::
:::

:::{#exr-area-superficie}
Una tolva tiene forma cónica dada por la función $f(x,y)=2\sqrt{x^2+y^2}$ con altura 4 m. Calcular el volumen de la tolva y la cantidad de chapa necesaria para construirla.

:::{.callout-note collapse="true"}
## Ayuda
El área de la superficie de una función $f(x,y)$ sobre una región $R$ puede calcularse mediante la integral 

$$
A_{R}(f) = \int_R \sqrt{f'_x(x_i,y_j)^2 + f'_y(x_i,y_j)^2 + 1}\,dA.
$$
:::

:::{.callout-tip collapse="true"}
## Solución
Para calcular el volumen es más sencillo trabajar en coordenadas polares.

```{julia}
using SymPy
@syms x y r θ
f(x, y) = 2*sqrt(x^2+y^2)
g(r, θ) = f(r*cos(θ), r*sin(θ))
solr = solve(g(r,θ)-4, r)
integrate(4 - g(r,θ)*r, (r, 0, solr[2]), (θ, 0, 2PI))
```

Para calcular en área de la superficie trabajamos en coordenadas rectangulares.

```{julia}
soly = solve(f(x,y)-4, y)
solx = solve(f(x,0)-4)
integrate(sqrt(diff(f(x,y), x)^2 + diff(f(x,y), y)^2 +1), (y, soly[1], soly[2]), (x, solx[1], solx[2]))
```
:::
:::

:::{#exr-centro-masas-placa-metalica}
Una placa metálica delimitada por las curvas $y=2-x^2$ e $y=-3+2x^2$ tiene una densidad dada por la función $d(x,y)=x^2y^2$.

a.  Calcular la masa de la placa.

    :::{.callout-note collapse="true"}
    ## Ayuda
    La masa de una región plana con densidad variable $\delta(x,y)$ se calcula mediante la integral.

    $$
    \int_R \delta(x,y)\,dA.
    $$
    :::

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    using SymPy
    @syms x y
    d(x,y) = x^2 * y^2
    g(x) = 2 - x^2
    h(x) = -3 + 2x^2
    sol = solve(g(x)-h(x), x)
    m = integrate(d(x,y), (y, h(x), g(x)), (x, sol[1], sol[2]))
    ```
    :::

a.  Calcular el centro de masas de la placa

    :::{.callout-note collapse="true"}
    ## Ayuda
    Las coordenadas del centro de masas se obtienen mediante las siguientes integrales.

    \begin{align*}
    \bar x 
    &= \frac{M_y}{m}
    = \frac{\int_R x\delta(x,y)\,dA}{\int_R \delta(x,y)\,dA} \\
    \bar y
    &= \frac{M_x}{m}
    = \frac{\int_R y\delta(x,y)\,dA}{\int_R \delta(x,y)\,dA}
    \end{align*}
    :::

    :::{.callout-tip collapse="true"}
    ## Solución

    Calculamos primero la componente $x$ del centro de masas. 

    ```{julia}
    # Momento con respecto a y
    my = integrate(x*d(x,y), (y, h(x), g(x)), (x, sol[1], sol[2]))
    # Centro de masas en x.
    my / m
    ```

    Y después la componente $y$ del centro de masas.

    ```{julia}
    # Momento con respecto a x
    mx = integrate(y*d(x,y), (y, h(x), g(x)), (x, sol[1], sol[2]))
    # Centro de masas en y.
    mx / m
    ```
    :::
:::

## Ejercicios propuestos

```{julia}
#| echo: false
#| results: "hidden"
using QuizQuestions, SymPy
nothing
```

:::{#exr-integrales-funciones-varias-variables-propuesto-1}
Calcular la integral $\int_{-1}^1\int_{-2y}^{2} e^{x^2}\,dx\,dy$

```{julia}
#| echo: false
using SymPy
@syms x, y::real
f(x, y) = exp(x^2)
sol = N(integrate(f(x,y), (y, -x/2, 1), (x, -2, 2)))
numericq(sol, 1e-5, hint="Introducir hasta 5 decimales")
```
:::

:::{#exr-integrales-funciones-varias-variables-propuesto-2}
Calcular el volumen de la región encerrada por las superficies $z^2=4-x$, $y^2=4-x$ y $x=0$.

```{julia}
#| echo: false
using SymPy
@syms x, y, z::real
solz = solve(z^2-4+x, z)
soly = solve(y^2-4+x, y)
solx = solve(-4+x)
sol = N(integrate(solz[2]-solz[1], (y, soly[1], soly[2]), (x, 0, solx[1])))
numericq(sol, 1e-5, hint="Introducir hasta 5 decimales")
```
:::

:::{#exr-integrales-funciones-varias-variables-propuesto-3}
Calcular el volumen encerrado por la superficie de la función $f(x,y)=\ln(\cos(x+y)+2)$ y el plano $z=0$ en la región delimitada por la circunferencia de radio $1$ centrada en el origen de coordenadas.

```{julia}
#| echo: false
using HCubature
f(x, y) = x^2+y^2 <= 1 ? log(cos(x*y)+2) : 0
f(v) = f(v...)
#sol = hcubature(f, [-1,-1], [1, 1])
numericq(3.42958314, 1e-5, hint="Introducir hasta 5 decimales")
```
:::

:::{#exr-integrales-funciones-varias-variables-propuesto-4}
Calcular el area de la superficie de la función $f(x,y)=\sqrt{x+y}$ en el intervalo $[0,1]\times[0,1]$.

```{julia}
#| echo: false
using SymPy, HCubature
@syms x y
f(x, y) = sqrt(x+y)
#sol = N(integrate(sqrt(diff(f(x,y), x)^2 + diff(f(x,y), y)^2 +1), (x, 0, 1), (y, 0, 1)))
numericq(1.2823707050179478, 1e-5, hint="Introducir hasta 5 decimales")
```
:::

:::{#exr-integrales-funciones-varias-variables-propuesto-5}
Calcular el centro de masas de la región plana delimitada por las funciones $y=\sqrt{x}$ e $y=x^2$ con densidad $d(x,y)= \ln(x+2y+1)$.

:::{.columns}
:::{.column}
Coordenada x
```{julia}
#| echo: false
# using SymPy
# @syms x y
# f(x, y) = ln(x+2y+1)
# m = integrate(f(x,y), (y, x^2, sqrt(x)), (x, 0, 1))
# my = integrate(x * f(x,y), (y, x^2, sqrt(x)), (x, 0, 1))
# sol = N(my / m)
numericq(0.5313566449, 1e-5, hint="Introducir hasta 5 decimales")
```
:::

:::{.column}
Coordenada y
```{julia}
#| echo: false
# mx = integrate(y * f(x,y), (y, x^2, sqrt(x)), (x, 0, 1))
# sol = N(mx / m)
numericq(0.5354903136, 1e-5, hint="Introducir hasta 5 decimales")
```
:::
:::
:::



