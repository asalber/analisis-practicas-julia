---
title: Derivadas de funciones de varias variables
lang: es
#jupyter: julia-1.9
---

## Ejercicios Resueltos

Para la realización de esta práctica se requieren los siguientes paquetes:

```{julia}
using SymPy  # Para el cálculo simbólico.
using Plots  # Para el dibujo de gráficas.
using Makie, GLMakie # Para el dibujo de gráficas en 3d.
using ImplicitPlots # Para el dibujo de gráficas de funciones implícitas.
using LaTeXStrings  # Para usar código LaTeX en los gráficos.
using LinearAlgebra # Para el módulo y el producto escalar de vectores.
```

:::{#exr-superficies}
Dibujar las gráficas de las siguientes funciones.

a.  $f(x,y) = 2x + 3y$.

    :::{.callout-note collapse="true"}
    ## Ayuda
    Usar la función [`surface`](https://aprendeconalf.es/manual-julia/graficos.html#gr%C3%A1ficos-en-el-espacio-real) del paquete `Plots` o la función [`surface`](https://aprendeconalf.es/manual-julia/graficos.html#superficies) del paquete `Makie` para representar superficies de funciones de dos variables.
    :::

    :::{.callout-tip collapse="true"}
    ## Solución

    :::{.panel-tabset}
    ## Plots
    ```{julia}
    using Plots
    f(x, y) = x/2 - 2y
    xs = ys = range(0, 2, 2)
    Plots.surface(xs, ys, f, xlab = "x", ylab = "y", zlab = "z")
    ```

    ## Makie

    ```{julia}
    using GLMakie
    f(x, y) = x/2 - 2y
    xs = ys = range(0, 2, 2)
    Makie.surface(xs, ys, f)
    ```
    :::
    :::

a.  $g(x) = x^2 - y^2$.

    :::{.callout-tip collapse="true"}
    ## Solución

    :::{.panel-tabset}
    ## Plots
    ```{julia}
    using Plots
    g(x, y) = x^2-y^2
    xs = ys = range(-1, 1, 30)
    Plots.surface(xs, ys, g, xlab = "x", ylab = "y", zlab = "z")
    ```

    ## Makie

    ```{julia}
    using GLMakie
    g(x, y) = x^2 - y^2
    xs = ys = range(-1, 1, 30)
    Makie.surface(xs, ys, g)
    ```
    :::
    :::

a.  $h(x) = \dfrac{\operatorname{sen}(x^2+y^2)}{\sqrt{x^2+y^2}}$

    :::{.callout-tip collapse="true"}
    ## Solución

    :::{.panel-tabset}
    ## Plots
    ```{julia}
    using Plots
    h(x, y) = sin(x^2+y^2)/sqrt(x^2+y^2)
    xs = ys = range(-pi, pi, 80)
    Plots.surface(xs, ys, h, xlab = "x", ylab = "y", zlab = "z")
    ```

    ## Makie

    ```{julia}
    using GLMakie
    h(x, y) = sin(x^2+y^2)/sqrt(x^2+y^2)
    xs = ys = range(-pi, pi, 80)
    Makie.surface(xs, ys, h)
    ```
    :::
    :::
:::

:::{#exr-curvas-nivel}
Dibujar las curvas de nivel de las siguientes funciones.

a.  $f(x,y) = 2x + 3y$.

    :::{.callout-note collapse="true"}
    ## Ayuda
    Usar la función [`contour`](https://docs.juliaplots.org/stable/series_types/contour/) del paquete `Plots` o la función [`contour`](https://docs.makie.org/stable/reference/plots/contour/index.html) del paquete `Makie` para representar curvas de nivel de funciones de dos variables.
    :::

    :::{.callout-tip collapse="true"}
    ## Solución

    :::{.panel-tabset}
    ## Plots
    ```{julia}
    using Plots
    f(x, y) = x/2 - 2y
    xs = ys = range(0, 2, 50)
    Plots.contour(xs, ys, f, levels = 7, xlab = "x", ylab = "y", clabels = true)
    ```

    ## Makie

    ```{julia}
    using GLMakie
    f(x, y) = x/2 - 2y
    xs = ys = range(0, 2, 50)
    Makie.contour(xs, ys, f, labels = true,  levels = 0:-0.5:-3)
    ```
    :::
    :::

a.  $g(x) = x^2 - y^2$.

    :::{.callout-tip collapse="true"}
    ## Solución

    :::{.panel-tabset}
    ## Plots
    ```{julia}
    using Plots
    g(x, y) = x^2 - y^2
    xs = ys = range(-1, 1, 100)
    Plots.contour(xs, ys, g, levels = 9, xlab = "x", ylab = "y", clabels = true)
    ```

    ## Makie

    ```{julia}
    using GLMakie
    f(x, y) = x^2 - y^2
    xs = ys = range(-1, 1, 100)
    Makie.contour(xs, ys, f, labels = true,  levels = -1:0.2:1)
    ```
    :::
    :::

a.  $h(x) = \dfrac{\operatorname{sen}(x^2+y^2)}{\sqrt{x^2+y^2}}$

    :::{.callout-tip collapse="true"}
    ## Solución

    :::{.panel-tabset}
    ## Plots
    ```{julia}
    using Plots
    h(x, y) = sin(x^2+y^2)/sqrt(x^2+y^2)
    xs = ys = range(-pi, pi, 80)
    Plots.contour(xs, ys, h, xlab = "x", ylab = "y")
    ```

    ## Makie

    ```{julia}
    using GLMakie
    h(x, y) = sin(x^2+y^2)/sqrt(x^2+y^2)
    xs = ys = range(-pi, pi, 80)
    Makie.contour(xs, ys, h, labels = true,  levels = -1:0.2:1)
    ```
    :::
    :::
:::

:::{#exr-superficie-curvas-nivel}
Dibujar en una misma gráfica la superficie de la función $f(x,y) = -4x e^{-x^2-y^2}$ y sus curvas de nivel.

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using GLMakie
f(x, y) = -4x*exp(-x^2-y^2)
xs = ys = range(-2, 2, 50)
zs = f.(xs, ys')
zmin = minimum(zs)
fig = Figure()
ax = Axis3(fig[1,1], )
surf = Makie.surface!(ax, xs, ys, zs, transparency = true)
Makie.wireframe!(ax, xs, ys, zs, color = (:black, 0.1), transparency = true)
Makie.contour!(ax, xs, ys, zs, levels = 20, transformation = (:xy, zmin), transparency=true)
Colorbar(fig[1, 2], surf, height = Relative(0.5))
fig
```
:::
:::


:::{#exr-derivada-parcial}
Sea $f(x,y)= x^2-2y^2$.

a.  Dibujar la gráfica y el punto $(1, 1, f(1,1))$.

    :::{.callout-note collapse="true"}
    ## Ayuda
    Usar la función [`surface`](https://aprendeconalf.es/manual-julia/graficos.html#superficies) del paquete `Makie` para representar superficies de funciones de dos variables. 

    Usar la función [`scatter`](https://aprendeconalf.es/manual-julia/graficos.html#diagrama-de-puntos) del paquete Makie para dibujar puntos.
    :::

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using GLMakie
    f(x, y) = x^2 - 2y^2
    xs = ys = range(-2, 2, 30)
    fig = Figure()
    ax = Axis3(fig[1,1], azimuth = pi/4)
    Makie.surface!(ax, xs, ys, f, transparency = true)
    Makie.scatter!(ax, Point3(1, 1, f(1,1)), color = :red)
    fig
    ```
    :::

a.  Dibujar el plano $y=1$ sobre la superficie. ¿Cuál es la curva que resulta de la intersección del plano con la superficie? ¿Cómo es la pendiente de la tangente a esta curva en $x=1$?

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    xs = [-2 -2; 2 2]
    zs = [-6 2; -6 2]
    ys = [1 1; 1 1]
    Makie.surface!(ax, xs, ys, zs; colormap = [:orange])
    fig
    ```

    La curva resultante es la parábola $f(x) = x^2$. La pendiente de la recta tangente a esta curva en $x=1$ vale $2$.
    :::

a.  Calcular la derivada parcial de $f$ con respecto a $x$ en el punto $(1,1)$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using SymPy
    @syms x y
    subs(diff(f(x,y), x), x => 1, y => 1)
    ``` 
    :::

a.  Dibujar el plano $x=1$ sobre la superficie. ¿Cuál es la curva que resulta de la intersección del plano con la superficie? ¿Cómo es la pendiente de la tangente a esta curva en $y=1$?

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    ys = [-2 -2; 2 2]
    zs = [-6 2; -6 2]
    xs = [1 1; 1 1]
    Makie.surface!(ax, xs, ys, zs; colormap = [:magenta])
    fig
    ```

    La curva resultante es la parábola $g(y) = -2y^2$. La pendiente de la recta tangente a esta curva en $y=1$ vale $-4$.
    :::

a.  Calcular la derivada parcial de $f$ con respecto a $y$ en el punto $(1,1)$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using SymPy
    @syms x y
    subs(diff(f(x,y), y), x => 1, y => 1)
    ```
    :::
:::

:::{#exr-derivadas-parciales}
La presión $P$ de un gas perfecto depende de la temperatura $T$ y del volumen que ocupa $V$ según la función 

$$
P(T,V) = \frac{nRT}{V},
$$

donde $n$ y $R$ son constantes positivas propias de cada gas.

a.  Calcular la derivada parcial de la presión con respecto a la temperatura. ¿Cómo la interpretarías?

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using SymPy
    @syms n R T V
    diff(n*R*T/V, T)
    ```

    Como $n$, $R$ y $V$ son positivos, la derivada parcial es positiva, por lo que si se mantiene el volumen constante y empezamos a aumentar la temperatura, la presión aumentará.
    :::

a.  Calcular la derivada parcial de la presión con respecto al volumen. ¿Cómo la interpretarías?

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    diff(n*R*T/V, V)
    ```

    Como $n$, $R$, $T$ y $V$ son positivos, la derivada parcial es negativa, por lo que si se mantiene la temperatura constante y empezamos a aumentar el volumen, la presión disminuirá.
    :::
:::

:::{#exr-igualdad-derivadas-cruzadas}
Dada la función $f(x,y) = e^{x+y}\operatorname{sen}\left(\frac{x}{y}\right)$, calcular las siguientes derivadas parciales de segundo orden.

a.  $\dfrac{\partial^2f}{\partial y\partial x}$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using SymPy
    @syms x y
    f(x, y) = exp(x+y)sin(x/y)
    diff(f(x, y), x, y)
    ```
    :::

a.  $\dfrac{\partial^2f}{\partial x\partial y}$.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using SymPy
    @syms x y
    f(x, y) = exp(x+y)sin(x/y)
    diff(f(x, y), y, x)
    ```
    :::

a.  ¿Se cumple la igualdad de las derivadas cruzadas?

    :::{.callout-tip collapse="true"}
    ## Solución
    Si, $\dfrac{\partial^2f}{\partial y\partial x} = \dfrac{\partial^2f}{\partial x\partial y}$, porque ambas derivadas son continuas en todo su dominio.
    :::
:::

:::{#exr-gradiente}
La función $f(x, y, z) = e^{-x-2y-3z}$ da la temperatura en cada posición $(x,y,z)$ de una habitación. 

a.  Si un mosquito está en la posición $(1, 1, 1)$, ¿en qué dirección debe volar para que la temperatura decrezca lo más rápidamente posible? ¿Cuál será la tasa de variación de la temperatura si el mosquito se mueve en esa dirección? 

    :::{.callout-note collapse="true"}
    ## Ayuda
    Debe moverse en la dirección opuesta al [vector gradiente](https://aprendeconalf.es/analisis-manual/13-derivadas-funciones-varias-variables.html#vector-gradiente) de $f$.
    :::

    :::{.callout-tip collapse="true"}
    ## Solución

    :::{.panel-tabset}
    ## Solución 1

    ```{julia}
    using SymPy, LinearAlgebra
    @syms x y z
    f(x, y, z) = exp(-x-2y-3z)
    ex = diff.(f(x, y, z), [x, y, z])
    ∇f = lambdify(ex, (x, y, z))
    println("Dirección de máximo decrecimiento : $(-∇f(1, 1, 1))")
    println("Tasa de variación en esa dirección: $(-norm(∇f(1, 1, 1)))")
    ```

    ## Solución 2

    ```{julia}
    using SymPy, LinearAlgebra
    @syms x y z
    f(x, y, z) = exp(-x-2y-3z)
    ex = [diff(f(x,y,z), i) for i in (x, y, z)]
    ∇f = lambdify(ex, (x, y, z))
    println("Dirección de máximo decrecimiento : $(-∇f(1, 1, 1))")
    println("Tasa de variación en esa dirección: $(-norm(∇f(1, 1, 1)))")
    ```
    :::
    :::

a.  Si el mosquito se mueve siguiendo la dirección del vector $(2, -1, 3)$, ¿la temperatura aumenta o disminuye? ¿Cuál será la tasa de variación de la temperatura en esa dirección?

    :::{.callout-note collapse="true"}
    ## Ayuda
    La tasa de variación de la función viene dada por la [derivada direccional](https://aprendeconalf.es/analisis-manual/13-derivadas-funciones-varias-variables.html#derivada-direccional) de la función en el punto $(1, 1, 1)$ siguiendo la dirección del vector $(2, -1, 3)$.
    :::

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    dot(∇f(1, 1, 1), normalize([2, -1, 3]))
    ```

    La temperatura disminuye.
    :::
:::

:::{#exr-recta-normal-plano-tangente}
Calcular las ecuaciones de la recta normal y el plano tangente a la superficie $x+2y-\ln(z)+4=0$ en el punto $(0,-2,1)$, y dibujarlos.

:::{.callout-note collapse="true"}
## Ayuda
Usar la propiedad de que el vector gradiente de una función $f(x,y,z)$ es siempre normal a las curvas de nivel $f(x,y,z)=0$.
:::

:::{.callout-tip collapse="true"}
## Solución
Dibujamos primero la superficie. 

```{julia}
using SymPy, GLMakie
f(x, y, z) = x + 2y - log(z) + 4
@syms x y z
f1 = lambdify(solve(f(x,y,z), z)[1])
xs = range(-0.5, 0.5, 30)
ys = range(-2.5, -1.5, 30)
fig = Figure()
ax = Axis3(fig[1,1], aspect = :data)
Makie.surface!(ax, xs, ys, f1)
Makie.scatter!(ax, Point3(0, -2, 1), color = :red)
fig
```

Calculamos primero la ecuación de la recta normal.

```{julia}
using SymPy
ex = diff.(f(x, y, z), [x, y, z])
∇f = lambdify(ex, (x, y, z))
nl(t) = [0, -2, 1] + t*∇f(0, -2, 1) 
@syms t
println("Ecuación de la recta normal $(nl(t))")
```

Y la dibujamos sobre la misma gráfica de la superficie.

```{julia}
ts = range(-0.5, 0.5, 2)
points = Point3.(nl.(ts))
Makie.lines!(ax, points)
fig
```

A continuación calculamos la ecuación del plano tangente.

```{julia}
using LinearAlgebra
tp(x,y) = solve(dot(([x, y, z] - [0, -2, 1]), ∇f(0, -2, 1)), z)[1]
println("Ecuación del plano tangente z = $(tp(x,y))")
```

Y finalmente lo dibujamos en la misma gráfica de la superficie.

```{julia}
Makie.surface!(ax, xs, ys, tp, colormap = ["red"], alpha = 0.5, transparency = true)
fig
```
:::
:::

:::{#exr-derivadas-implicitas}
La ecuación $x^3+y^3=8$ define una curva implícita.

a.  Dibujar la gráfica de la ecuación.

    :::{.callout-note collapse="true"}
    ## Ayuda
    Usar la función [`implicit_plot`](https://juliapackages.com/p/implicitplots) del paquete `ImplicitPlots` para dibujar la gráfica de una función implícita definida por una ecuación.
    :::

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using Plots, ImplicitPlots, LaTeXStrings
    f(x, y) = x^3 + y^3 - 8 
    implicit_plot(f, xlab =  L"$x$", ylab = L"$y$", legend = false)
    ```
    :::

a.  Calcular la derivada de $y$ como función implícita de $x$.

    :::{.callout-note collapse="true"}
    ## Ayuda
    La derivada de $y$ como función implícita de $x$ en la ecuación $f(x,y)=0$ se puede calcular como

    $$
    \frac{dy}{dx} = -\frac{\partial f/\partial x}{\partial f/\partial y}
    $$
    :::

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    using SymPy
    @syms x y
    y′ = -diff(f(x, y), x) / diff(f(x, y), y)
    ```
    :::

a.  Calcular la ecuación de la recta tangente a la curva implícita en el punto $(\sqrt[3]{4}, \sqrt[3]{4})$.

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    x₀ = y₀ = 2^(2/3)
    tan = lambdify(y₀ + subs(y′, y => y₀, x => x₀)*(x-x₀))
    Plots.plot!(tan)
    ```
    :::

<!-- a.  Calcular la segunda derivada de $y$. ¿Cómo es la concavidad de la función en el punto $(\sqrt[3]{4}, \sqrt[3]{4})$? -->
:::

:::{#exr-extremos}
La función $f(x,y) = x^3+y^3-3xy$ determina la concentración de una sustancia $z$ en función de las concentraciones de otras dos $x$ e $y$ en una reacción química.

a.  Representar la gráfica de la función.

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using GLMakie
    f(x, y) = x^3+y^3-3x*y
    xs = ys = range(-0.5, 1.5, 30)
    Makie.surface(xs, ys, f)
    ```
    :::

a.  Calcular los puntos críticos de la función.

    :::{.callout-note collapse="true"}
    ## Ayuda
    Los puntos críticos son los puntos que anulan el gradiente. Para resolver el sistema de ecuaciones que resulta de igualar el vector gradiente al vector nulo se pueden utilizar las funciones [`linsolve`](https://docs.juliahub.com/SymPy/KzewI/1.2.1/Tutorial/solvers/#Solving-Equations-Algebraically) (para sistemas lineales) o [`nonlinsolve`](https://docs.juliahub.com/SymPy/KzewI/1.2.1/Tutorial/solvers/#Solving-Equations-Algebraically) (para sistemas no lineales) del paquete `SymPy`.
    :::

    :::{.callout-tip collapse="true"}
    ## Solución

    ```{julia}
    using SymPy
    @syms x y
    nonlinsolve(diff.(f(x, y), (x, y)), (x, y))
    ```
    :::

a.  Determinar los extremos relativos y los puntos de silla. ¿Para qué concentraciones de $x$ e $y$ la concentración de $z$ será mínima?

    :::{.callout-note collapse="true"}
    ## Ayuda
    Para [determinar los extremos relativos y los puntos de silla](https://aprendeconalf.es/analisis-manual/13-derivadas-funciones-varias-variables.html#thm-extremos-funcion-dos-variables) de una función de dos variables, hay que calcular el hessiano en los puntos críticos.
    :::

    :::{.callout-tip collapse="true"}
    ## Solución

    Definimos una función para el hessiano.
    ```{julia}
    using LinearAlgebra
    hes = lambdify(det(hessian(f(x,y), (x,y))))
    ```

    Calculamos el hessiano en el punto crítico $(0,0)$.

    ```{julia}
    hes(0,0)
    ```

    Como es negativo, en $(0,0)$ hay un punto de inflexión.

    Calculamos el hessiano en el punto crítico $(1,1)$.

    ```{julia}
    hes(1,1)
    ```

    Como es positivo, existe un extremo relativo en $(1,1)$. Para ver si se trata de un máximo o mínimo calculamos $\frac{\partial^2 f}{\partial x^2}$.

    ```{julia}
    fxx = lambdify(diff(f(x,y), x, x), (x,y))
    fxx(1,1)
    ```

    Como es positiva, en $(1,1)$ hay un mínimo. Así pues, la concentración de $z$ será mínima cuando las concentraciones de $x$ e $y$ sean 1.
    :::
:::

:::{#exr-polinomios-taylor}
Sea $f(x,y)=\sqrt{xy}$.

a.  Dibujar la gráfica de $f$ y el punto $(1, 1, f(1,1))$.

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    using GLMakie
    f(x, y) = sqrt(x*y)
    a = b = 1
    xs = ys = range(0.5, 2, 30)
    fig = Figure()
    ax = Axis3(fig[1,1], azimuth = -pi/4, elevation = 0.1)
    Makie.surface!(ax, xs, ys, f)
    Makie.scatter!(ax, Point3(a, b, f(a,b)), color = :red)
    fig
    ```
    :::

a.  Calcular el polinomio de Taylor de primer grado de $f$ en el punto $(1,1)$ y representarlo gráficamente.
Comprobar que se obtiene el plano tangente a la superficie de $f$ en el punto $(1,1)$.

    :::{.callout-note collapse="true"}
    ## Ayuda
    La fórmula del polinomio de Taylor de primer grado de la función $f(x,y)$ en el punto $(a,b)$ es

    $$
    P^1(x,y) = f(a,b) + \nabla f(a,b)(x-a, y-b).
    $$
    :::

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    using SymPy, LinearAlgebra
    @syms x, y
    ex = diff.(f(x, y), [x, y])
    ∇f = lambdify(ex, (x, y))
    p1(s,t) = f(a, b) + dot(∇f(a, b), [s-a, t-b])
    Makie.surface!(ax, xs, ys, p1; colormap = [:orange], transparency = true)
    fig
    ```
    :::

a.  Utilizar el polinomio anterior para calcular el valor aproximado de $\sqrt{1.01\cdot 0.99}$. ¿Cuál es el error cometido en la aproximación?

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    error = abs(f(1.01, 0.99) - p1(1.01, 0.99))
    ```
    :::

a.  Calcular el polinomio de Taylor de segundo grado de $f$ en el punto $(1,1)$ y representarlo gráficamente.

    :::{.callout-note collapse="true"}
    ## Ayuda
    La fórmula del polinomio de Taylor de segundo grado de la función $f(x,y)$ en el punto $(a,b)$ es

    $$
    P^2(x,y) = f(a,b) + \nabla f(a,b)(x-a, y-b) + \frac{1}{2!}\nabla^2f(a,b)(x-a,y-b)(x-a,y-b).
    $$
    :::

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    using SymPy, LinearAlgebra
    @syms x, y
    ∇²f = lambdify(hessian(f(x,y), (x, y)), (x, y))
    p2(s,t) = f(a, b) + dot(∇f(a, b), [s-a, t-b]) + 1/2 * dot(∇²f(a,b) * [s-a; t-b],  [s-a, t-b])
    Makie.surface!(ax, xs, ys, p2; colormap = [:magenta], transparency = true)
    fig
    ```
    :::

a.  Utilizar el polinomio anterior para calcular el valor aproximado de $\sqrt{1.01\cdot 0.99}$.
¿Cuál es error cometido en la aproximación? ¿Es mayor o menor que en la aproximación mediante el polinomio de Taylor de primer grado?

    :::{.callout-tip collapse="true"}
    ## Solución
    ```{julia}
    error = f(1.01, 0.99) - p2(1.01, 0.99) 
    ```
    :::
:::

## Ejercicios propuestos

```{julia}
#| echo: false
#| results: "hidden"
using QuizQuestions, SymPy
nothing
```

:::{#exr-derivadas-funciones-varias-variables-propuesto-1}
¿Emparejar las siguientes funciones de dos variables con sus diagramas de contorno?

:::{.content-visible when-format="html"}
![Curvas de nivel.](img/derivadas-funciones-varias-variables/curvas-nivel.svg)
:::

:::{.content-visible unless-format="html"}
![Curvas de nivel.](img/derivadas-funciones-varias-variables/curvas-nivel.pdf)
:::

```{julia}
#| hold: true
#| echo: false
d = Dict(L"$f(x,y) = \frac{\operatorname{sen}(2x^2+y^2)}{\sqrt{x^2+y^2}}$" => "A", L"$g(x,y) = \frac{\cos(xy)}{\sqrt{x^2+1}}$" => "B", L"$h(x,y) = \cos(x)-\operatorname{sen}(2y)$" => "C", L"$i(x,y) = \frac{1}{2}(\operatorname{sen}(x^2)-\cos(y^2))$" => "D")
matchq(d, label="Empareja la función con su gráfica.")
```
:::

:::{#exr-derivadas-funciones-varias-variables-propuesto-2}
Dada la función 

$$
f(x,y,z)=\frac{\ln(\sqrt{\cos(x^2y)})}{z^2},
$$

calcular $\dfrac{\partial^3 f}{\partial y\partial z\partial x}$ en el punto $(2,\pi,1)$.

```{julia}
#| echo: false
using SymPy
@syms x, y, z::real
f(x,y,z) = log(sqrt(cos(x^2*y)))/z^2
sol = N(subs(diff(f(x,y,z), x, z, y), x=>2, y=>pi, z=>1))
numericq(sol, 1e-5, hint="Introducir hasta 5 decimales")
```
:::

:::{#exr-derivadas-funciones-varias-variables-propuesto-3}
La velocidad de una reacción química típicamente depende de la temperatura y de la concentración de los reactivos. En una reacción de hidrólisis del acetato de etilo por hidróxido de sodio (NaOH) en una solución acuosa, la velocidad de reacción viene dada por la fórmula $R(t,a,s)=\frac{t}{3}\sqrt[3]{a^2s^2}$, donde $t$ es la temperatura de la reacción, $a$ la concentración de acetato de etilo y $s$ la concentración de hidróxido de sodio.

a.  ¿En qué dirección debe cambiarse la temperatura y las concentraciones de los reactivos para que la velocidad de reacción aumente lo más rápidamente posible si la temperatura y las concentraciones actuales son the 40ºC, 0.4 mol/l y 0.5 mol/l respectivamente?

    ```{julia}
    #| echo: false
    using SymPy
    @syms x, y, z::real
    f(x,y,z) = x/3 * (y*z)^(2/3)
    ex = diff.(f(x, y, z), [x, y, z])
    ∇f = lambdify(ex, (x, y, z))
    sol = ∇f(40, 0.4, 0.5)
    fillblankq("Dirección de cambio de la temperatura: ____" , sol[1])
    ```

    ```{julia}
    #| echo: false
    fillblankq("Dirección de cambio del acetato de etilo: ____" , sol[2])
    ```

    ```{julia}
    #| echo: false
    fillblankq("Dirección de cambio del hidróxido de sodio: ____" , sol[3])
    ```

a.  ¿Cómo cambiará la velocidad de reacción en ese instante si la temperatura disminuye al mismo ritmo que aumenta la concentración del acetato de etilo y la concentración de acetato de etilo aumenta la mitad de lo que aumenta la concentración del hidróxido de sodio?

    ```{julia}
    #| echo: false
    using LinearAlgebra
    sol = dot(∇f(40, 0.4, 0.5), normalize([-1/2, 1/2, 1]))
    numericq(sol, 1e-5, hint="Introducir hasta 5 decimales")
    ```
:::

:::{#exr-derivadas-funciones-varias-variables-propuesto-4}
La superficie $\frac{e^{z\cos(xy)}}{xz}=\frac{1}{e}$ define a $z$ como función implícita $z=f(x,y)$.

a.  ¿Cuál de las siguientes afirmaciones es cierta en el punto $(1,\pi,1)$?

    ```{julia}
    #| echo: false  
    using SymPy, LinearAlgebra
    @syms x, y, z::real
    f(x,y,z) = exp(z*cos(x*y))/(x*z)-1/ℯ
    fx = diff.(f(x,y,z), x)
    fy = diff.(f(x,y,z), y)
    fz = diff.(f(x,y,z), z)
    - subs(fx, x=>1, y=>pi, z=>1) / subs(fz, x=>1, y=>pi, z=>1)
    choices = (L"La variable $z$ aumenta $1/e$ unidades por cada unidad que aumente $x$, si $y$ se mantiene constante.", L"La variable $z$ disminuye la mitad de lo que aumenta $x$, si $y$ se mantiene constante.", L"La variable $z$ disminuye lo mismo que aumenta $y$, si $x$ se mantiene constante.", L"La variable $z$ se mantiene constante cuando se incrementa $x$, si $y$ se mantiene constante.", "Las otras opciones son falsas.")
    answer = 2
    radioq(choices, answer)
    ```

a.  ¿Cuál es la ecuación del plano tangente en el punto $(1,\pi,1)$?

    ```{julia}
    #| echo: false        
    ex = diff.(f(x, y, z), [x, y, z])
    ∇f = lambdify(ex, (x, y, z))
    tp(x,y) = solve(dot(([x, y, z] - [1, PI, 1]), ∇f(1, PI, 1)), z)[1]
    choices = (L"$z=1-x+\frac{y}{\pi}$", L"$z=\frac{3-x}{2}$", L"$z=(\pi+1)x-y$", L"$z=x+y-(\pi+2)$", "Las otras opciones son falsas.")
    answer = 2
    radioq(choices, answer)
    ```
:::

:::{#exr-derivadas-funciones-varias-variables-propuesto-5}
Dada la función $f(x,y)=-\dfrac{y}{9+x^2+y^2}$, ¿cuál de las siguientes afirmaciones es cierta?

```{julia}
#| echo: false     
using SymPy, LinearAlgebra
@syms x, y::real
f(x,y) = -y/(9+x^2+y^2)
nonlinsolve(diff.(f(x, y), (x, y)), (x, y))
choices = (L"Tiene un máximo relativo en el punto $(3,-3)$.", L"Tiene un máximo relativo en el punto $(0,3)$ y un mínimo relativo en el punto $(0,-3)$.", L"Tiene un máximo relativo en el punto $(0,-3)$ y un punto de silla en el punto $(0,3)$.", "No tiene extremos relativos.", L"Tiene un máximo relativo en el punto $(0,-3)$ y un mínimo relativo en el punto $(0,3)$.")
answer = 5
radioq(choices, answer)
```
:::

:::{#exr-derivadas-funciones-varias-variables-propuesto-6}
Calcular el polinomio de Taylor de segundo grado de la función $f(x,y)=\operatorname{sen}(x/y)$ en el punto $(\pi/2,1)$ y utilizarlo para calcular el valor aproximado de $f(1.5,1)$.

```{julia}
#| echo: false  
sol = 0.99749394
numericq(sol, 1e-8, hint="Introducir hasta 8 decimales")
```
:::

