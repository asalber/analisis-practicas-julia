---
lang: es
jupyter: julia-1.8
---
<!-- LTeX: language = es-ES -->

# Funciones elementales

## Ejercicios Resueltos

Para la realización de esta práctica se requieren los siguientes paquetes:

```{julia}
using Plots  # Para el dibujo de gráficas.
#plotlyjs()  # Para obtener gráficos interactivos.
using SymPy # Para el cálculo simbólico.
using MTH229 # Para restringir la gráfica de una función a su dominio.
using LaTeXStrings  # Para usar código LaTeX en los gráficos.
```

:::{#exr-grafica-funcion-tabla}
La siguiente tabla contiene el número de bacterias en un cultivo cada hora que pasa.

| Horas | Bacterias |
|:-:|:-:|
| 0 | 1 |
| 1 | 2 |
| 2 | 4 |
| 3 | 8 |
| 4 | 16 |
| 5 | 32 |
| 6 | 64 |
| 7 | 128 |

Dibujar una gráfica con la evolución del la población de bacterias.

:::{.callout-note collapse="true"}
## Ayuda
Definir los valores de las horas en un vector `x` y el número de bacterias en otro `y` y luego utilizar la función  [`scatter(x,y)`](https://aprendeconalf.es/manual-julia/graficos.html#a%C3%B1adir-puntos-a-una-gr%C3%A1fica) del paquete `Plots` para dibujar una gráfica de puntos.
:::

:::{.callout-tip collapse="true"}
## Solución

```{julia}
using Plots
horas = 0:7
bacterias = [1, 2, 4, 8, 16, 32, 64, 128]
scatter(horas, bacterias, xlab="Horas", ylab="Bacterias", title="Evolución de la población de bacterias", legend=false)
```
:::

¿Los pares dados en la tabla forman una función?

:::{.callout-tip collapse="true"}
## Solución
Si, porque para cada hora hay a lo sumo un número de bacterias con el que se relaciona.
:::

¿Qué fórmula crees que explica la evolución del número de bacterias en función de las horas que pasan? Dibuja en la gráfica anterior la función con esa fórmula.

:::{.callout-note collapse="true"}
## Ayuda
Para añadir una nueva gráfica a una anterior se añade un signo de exclamación `!` a la función de graficación. Para una gráfica de líneas, utilizar la función  [`plot!`](https://aprendeconalf.es/manual-julia/graficos.html#gr%C3%A1fica-de-una-funci%C3%B3n-de-una-variable) del paquete `Plots`, pasándole el nombre de la función si se ha definido previamente o la [definición anónima de la función](https://aprendeconalf.es/manual-julia/funciones.html#funciones-an%C3%B3nimas) 
:::

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using Plots
plot!(x -> 2^x)
```
:::
:::

:::{#exr-resolucion-grafica-ecuacion}
En el lanzamiento vertical de un objeto, la posición que ocupa el objeto en cada instante $t$, viene dado por la función

$$
y(t) =  y_0+v_0t+\frac{1}{2}gt^2
$$

donde $y_0$ es la altura inicial del objeto, $v_0$ la velocidad inicial con que se lanza, y $g$ es la aceleración de la gravedad. Una pelota se lanza verticalmente desde la ventada de un edificio a 5 m de altura, con una velocidad inicial de $10$ m/s. Dibujar la gráfica de la posición de la pelota en función del tiempo, tomando una aceleración de la gravedad $g=-9.8$ m/s².

:::{.callout-note collapse="true"}
## Ayuda
Declarar $t$ como una variable simbólica usando el paquete [`SymPy`](https://docs.juliahub.com/SymPy/KzewI/1.0.29/introduction/#Symbols-1), definir después las constantes $y_0$, $v_0$ y $g$, y luego definir la función $y(t)$ mediante la fórmula dada.

Para dibujar la gráfica de la función, usar la función `plot` pasándole el nombre de la función. Como no tienen sentido los instantes negativos, ni las posiciones negativas, restringir la ventana de graficación a valores de $t$ y de $y$ positivos usando los parámetros [`xlim`](https://aprendeconalf.es/manual-julia/graficos.html#ventana-de-graficaci%C3%B3n) e [`ylim`](https://aprendeconalf.es/manual-julia/graficos.html#ventana-de-graficaci%C3%B3n).
:::

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using Plots, SymPy
@vars t  #Declaramos t como una variable simbólica
y₀ = 5
v₀ = 10
const gravedad = -9.8  #Declaramos la gravedad como una constante
y0(t) = y₀+v₀*t+ 1/2*gravedad*t^2
plot(y0, xlims=(0,3), ylims=(0,15), label="Pelota", xlab="Tiempo (s)", ylab="Altura (m)")
```
:::

Al mismo tiempo que se lanza la pelota, un ascensor exterior baja por la fachada del mismo edificio desde una altura de $8$ m con una velocidad constante de $5$ m/s. Dibujar la gráfica de la posición del ascensor junto a la gráfica de la pelota.

:::{.callout-note collapse="true"}
## Ayuda
La función que define la posición del ascensor que baja desde una altura $y_1$ con una velocidad constante $v_1$ en cada instante $t$ es $y(t) = y_1-v_1t$.
:::

:::{.callout-tip collapse="true"}
## Solución
```{julia}
y₁ = 8
v₁ = 2
y1(t) = y₁-v₁*t
plot!(y1, label="Ascensor")
```
:::

¿En qué instantes el ascensor estará a la misma altura de la pelota? Dibujar los puntos correspondientes a esos instantes en la gráfica anterior.

:::{.callout-tip collapse="true"}
## Solución
```{julia}
sol = solve(y0(t)-y1(t))
print("Instantes: ", sol)
scatter!(sol, y1.(sol), label="Intersección")
```
:::
:::

:::{#exr-grafica-funcion-polinomica}
El volumen de un globo esférico depende del radio según la función $v(r)= \frac{4}{3}\pi r^3$. Calcular la función que expresa el radio en función del volumen y dibujar su gráfica.

:::{.callout-note collapse="true"}
## Ayuda
Declarar las variables simbólicas `r` y `v` usando el paquete `SymPy` y definir la función `vol(r)` que expresa el volumen del globo en función del radio.

Después utilizar la función [`solve`](https://docs.juliahub.com/SymPy/KzewI/1.0.29/introduction/#The-solve-function-1) del paquete `SymPy` para despejar `r` de la ecuación `v-vol(r)=0`.
:::

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using Plots, SymPy
@vars r v
vol(r) = 4/3*pi*r^3
rad = solve(v-vol(r),r)[1]
plot(rad, xlab="Volumen (m³)", ylab="Radio (m)", legend=false)
```
:::

Si empezamos a introducir helio en el globo de manera que su volumen a los $t$ minutos viene dado por la función $v(t)=t^2+2t$, dibujar la gráfica de la función que da el radio en cada instante.

:::{.callout-note collapse="true"}
## Ayuda
Declarar las variables simbólicas `t` usando el paquete `SymPy` y definir la función `vol(t)` que expresa el volumen del globo en función del tiempo.

Después utilizar a el operador de composición $\circ$ para componer la función del volumen con la función del radio.
:::

:::{.callout-tip collapse="true"}
## Solución
```{julia}
@vars t
vol(t)=t^2+2t
plot(rad∘vol, xlim=(0,10), xlab="Tiempo (min)", ylab="Radio (m)")
```
:::

Si el globo explota cuando el radio alcanza los 3 m, ¿cuándo explotará el globo?

:::{.callout-tip collapse="true"}
## Solución
```{julia}
sol = solve((rad∘vol)(t)-3)
```

```{julia}
#| echo: false
using Markdown

md"El globo explotará a los $(sol[2]) minutos."
```
:::
:::

:::{#exr-grafica-funcion-polinomica}
Dibujar la gráfica de la función 

$$
f(x) = 2x^3-3x^2-12x+4
$$

en el intervalo $[-3,4]$ y determinar, observando la gráfica, lo siguiente:

:::{.callout-note collapse="true"}
## Ayuda
Definir la función y usar la función [`plot`](https://docs.juliaplots.org/latest/tutorial/#Basic-Plotting:-Line-Plots) del paquete `Plots`.
:::

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using Plots, LaTeXStrings
f(x) = 2x^3-3x^2-12x+4
plot(f, -3, 4, label=L"$f(x) = 2x^3-3x^2-12x+4$")
```
:::

a. Dominio

:::{.callout-tip collapse="true"}
## Solución
$Dom(f)=\mathbb{R}$
:::

b. Imagen

:::{.callout-tip collapse="true"}
## Solución
$Im(f)=\mathbb{R}$
:::

c. Raíces

:::{.callout-tip collapse="true"}
## Solución

```{julia}
using SymPy
@syms x
f(x) = 2x^3-3x^2-12x+4
raices = solve(f(x))  # Solución exacta
print(raices)
N(raices)  # Solución aproximada con decimales
```
Hay tres raíces en $x=-2$, $x=0.31$ y $x=3.19$ aproximadamente.
:::

d. Signo

:::{.callout-tip collapse="true"}
## Solución
Intervalos con $f(x)$ negativa: $(-\infty, -2)\cup (0.31,3.19)$.  
Intervalos con $f(x)$ positiva: $(-2,0.31)\cup (3.19,\infty)$.
:::

e. Crecimiento

:::{.callout-tip collapse="true"}
## Solución
Intervalos con $f(x)$ creciente: $(-\infty, -1)\cup (2,\infty)$.  
Intervalos con $f(x)$ decreciente: $(-1,2)$.
:::

f. Extremos

:::{.callout-tip collapse="true"}
## Solución
Máximo relativo en $x=-1$ y el valor máximo es $f(-1)=11$.  
Mínimo relativo en $x=2$ y el valor del mínimo es $f(2)=-16$.
:::

g. Concavidad

:::{.callout-tip collapse="true"}
## Solución
Intervalos de concavidad hacia arriba: $(0.5,\infty)$.  
Intervalos de concavidad hacia abajo: $(-\infty, 0.5)$.
:::

h. Puntos de inflexión

:::{.callout-tip collapse="true"}
## Solución
Punto de inflexión en $x=0.5$.
:::

:::


:::{#exr-grafica-funcion-racional}
Dibujar la gráfica de la función 

$$
g(t) = \frac{t^4+19t^2-5}{t^4+9t^2-10}
$$

en el intervalo $[-8,8]$ y determinar, observando la gráfica, lo siguiente:

:::{.callout-note collapse="true"}
## Ayuda
Usar la función `plot` como en el ejercicio anterior con el parámetro `aspect_ratio=1.0` para que los ejes tengan la misma escala. 

Para respetar las discontinuidades autilizar la función `rangeclamp()` del paquete `MTH229`.
:::

:::{.callout-tip collapse="true"}
## Solución
```{julia}
g(t) = (t^4+19t^2-5) / (t^4+9t^2-10)
plot(rangeclamp(g), -8, 8, aspect_ratio=1.0, ylims=(-5,5) , xticks =Vector(-10:10), yticks = Vector(-5:5), label=L"$g(t) = \frac{t^4+19t^2-5}{t^4+9t^2-10}$")
```
:::

a. Dominio. ¿Qué pasa si aplicamos la función a algún valor fuera de su dominio?

:::{.callout-tip collapse="true"}
## Solución
$Dom(f)=\mathbb{R}\setminus\{-1,1\}$

```{julia}
g(-1), g(1)
```

Como se observa, al aplicar la función a $-1$ y $1$ se obtiene $\infty$.
:::

b. Imagen

:::{.callout-tip collapse="true"}
## Solución
$Im(f)=\mathbb{R}\setminus (0.5,1]$
:::

c. Asíntotas. Dibujarlas.

:::{.callout-note collapse="true"}
## Ayuda
Buscar las asíntotas verticales en los puntos fuera del dominio de la función.

Para dibujar asíntotas verticales usar la función [`vline`](https://docs.juliaplots.org/latest/api/#Plots.vline-Tuple) del paquete `Plots`, y para dibujar las asíntotas horizontales usar la función [`hline`](https://docs.juliaplots.org/latest/api/#Plots.hline-Tuple).
:::

:::{.callout-tip collapse="true"}
## Solución
```{julia}
vline!([-1,1], label="Asíntotas verticales")
hline!([1], label="Asíntotas horizontales", legend=:bottomright)
```
Asíntotas verticales en $x=-1$ y $x=1$.  
Asíntotas horizontales en $y=1$.  
No hay asíntotas oblicuas.
:::

d. Raíces

:::{.callout-tip collapse="true"}
## Solución
Hay dos raíces en $x=-0.5$ y $x=0.5$ aproximadamente.
:::

e. Signo

:::{.callout-tip collapse="true"}
## Solución
Intervalos con $f(x)$ positiva: $(-\infty,-1)\cup (-0.5,0.5)\cup (1,\infty)$.  
Intervalos con $f(x)$ negativa: $(-1,-0.5)\cup (0.5,1)$.
:::

e. Crecimiento

:::{.callout-tip collapse="true"}
## Solución
Intervalos con $f(x)$ creciente: $(-\infty, -1)\cup (-1,0)$.  
Intervalos con $f(x)$ decreciente: $(0,1)\cup (1,\infty)$.
:::

f. Extremos

:::{.callout-tip collapse="true"}
## Solución
Máximo relativo en $x=0$ y el valor máximo es $g(0)=0.5$.  
No hay mínimos relativos.
:::

g. Concavidad

:::{.callout-tip collapse="true"}
## Solución
Intervalos de concavidad hacia arriba: $(-\infty,-1)\cup (1,\infty)$.  
Intervalos de concavidad hacia abajo: $(-1,1)$.
:::

h. Puntos de inflexión

:::{.callout-tip collapse="true"}
## Solución
No hay puntos de inflexión.
:::

:::

:::{#exr-funciones-exponenciales}
Dibujar la gráficas de las siguientes funciones exponenciales $2^x$, $e^x$, $0.5^x$, $0.7^x$ y responder a las siguientes preguntas comparando las gráficas.

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using Plots, LaTeXStrings
plot(2^x, -2, 2, label=L"$2^x$")
plot!(exp(x), label=L"$e^x$")
plot!(0.5^x, label=L"$2^x$")
plot!(0.7^x, label=L"$2^x$")
```
:::

a. ¿Cuál es el dominio de una función exponencial?

:::{.callout-tip collapse="true"}
## Solución
$\mathbb{R}$.
:::

b. ¿Cuál es la imagen de una función exponencial?

:::{.callout-tip collapse="true"}
## Solución
$\mathbb{R}^+$.
:::

c. ¿Cómo es el crecimiento de una función exponencial?

:::{.callout-tip collapse="true"}
## Solución
$a^x$ es creciente si $a>1$ y decreciente si $0<a<1$.
:::

d. ¿Tienen extremos una función exponencial?

:::{.callout-tip collapse="true"}
## Solución
No
:::

e. ¿Cómo es la curvatura de una función exponencial?

:::{.callout-tip collapse="true"}
## Solución
Es cóncava hacia arriba.
:::

:::

:::{#exr-funciones-trigonométricas}
Dibujar la gráficas de las funciones trigonométricas $\operatorname{sen}(x)$, $\operatorname{sen}(x+2)$, $\operatorname{sen}(x)+2$, $\operatorname{sen}(2x)$ y $2\operatorname{sen}(x)$, y completar la siguiente tabla estudiando su periodo y amplitud. 

| Función | Periodo | Amplitud |
|-|-|-| 
| $\operatorname{sen}(x)$ | | | 
| $\operatorname{sen}(x+2)$ | | | 
| $\operatorname{sen}(x)+2$ | | | 
| $\operatorname{sen}(2x)$ | | | 
| $2\operatorname{sen}(x)$ | | | 

¿Qué conclusiones sacas?

:::{.callout-note collapse="true"}
## Ayuda
El periodo es el mínimo intervalo en el que la gráfica de la función se repite, y la amplitud es la mitad de la diferencia entre el máximo y el mínimo de la función.
:::

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using Plots, LaTeXStrings
plot(sin(x), -2*pi, 2*pi, label=L"$\operatorname{sen}(x)$")
plot!(sin(x+2), label=L"$\operatorname{sen}(x+2)$")
plot!(sin(x)+2, label=L"$\operatorname{sen}(x)+2$")
plot!(sin(2x), label=L"$\operatorname{sen}(2x)$")
plot!(2sin(x), label=L"$2\operatorname{sen}(x)$")
```

| Función | Periodo | Amplitud |
|-|-|-| 
| $\operatorname{sen}(x)$ | $2\pi$ | 1 | 
| $\operatorname{sen}(x+2)$ | $2\pi$ | 1 | 
| $\operatorname{sen}(x)+2$ | $2\pi$ | 1 | 
| $\operatorname{sen}(2x)$ | $\pi$ | 1 | 
| $2\operatorname{sen}(x)$ | $2\pi$ | 2 | 
:::

Se observa que al sumar una constante a la función seno o a su argumento, el periodo y la amplitud no cambian. Sin embargo, si se multiplica por una constante el seno, cambia la amplitud, y si se multiplica su argumento, cambia el periodo.

:::

:::{#exr-funciones-a-trozos}
Dibujar la gráfica de la función a trozos

$$
h(x)=
\begin{cases}
-2x & \mbox{si } x\leq 0;\\
x^2 & \mbox{si } 0< x \leq 2;\\ 
4 & \mbox{si } 2< x
\end{cases}
$$

:::{.callout-note collapse="true"}
## Ayuda
Usar el [operador condicional](https://aprendeconalf.es/manual-julia/estructuras-control.html#operador-condicional) anidado.
:::

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using Plots
h(x) = x<=0 ? -2x : x<=2 ? x^2 : 4
plot(h, legend = false)
```
:::
:::


:::{#exr-funciones-parametricas}
Una hormiga se mueve sobre el plano real de manera que en cada instante $t$ su posición viene dada por las funciones

$$
\begin{cases}
x=\operatorname{sen}(t) \\
y=\operatorname{sen}(2t)
\end{cases}
$$

Dibujar la gráfica de la trayectoria de la hormiga.

:::{.callout-tip collapse="true"}
## Solución
```{julia}
using Plots
u1(t)=sin(t)
v1(t)=sin(2t)
plot(u1, v1, 0, 4pi, aspect_ratio=1.0, legend = false)
```
:::

:::


## Ejercicios propuestos
```{julia}
#| echo: false
#| results: "hidden"
using QuizQuestions
nothing
```

:::{#exr-dominio-imagen}
¿Cuáles de las siguientes funciones tienen dominio $\mathbb{R}$ e imagen $\mathbb{R}^+\cup\{0\}$?

```{julia}
#| hold: true
#| echo: false
choices = (L"$|x|$", L"$2^x$", L"$1/x^2$", L"$\sqrt(x)$", L"$x^4-2x^3+4x$")
answers = [1,5]
multiq(choices, answers)
```
:::

:::{#exr-funciones-logaritmicas}
Dibujar las gráficas de las funciones logarítmicas $\ln(x)$, $\log_2(x)$ y $\log_{0.5}(x)$ y contestar a las siguientes preguntas.

a. ¿Cuál es el dominio de una función logarítmica?

```{julia}
#| hold: true
#| echo: false
using LaTeXStrings
choices = (L"$\mathbb{R}$", L"$\mathbb{R}\setminus\{0\}$", L"$\mathbb{R}^+\cup \{0\}$", L"$\mathbb{R}^+$", "Las otras opciones son falsas." )
answer = 4
radioq(choices, answer)
```

b. ¿Cuál es la imagen de una función logarítmica?

```{julia}
#| hold: true
#| echo: false
using LaTeXStrings
choices = (L"$\mathbb{R}$", L"$\mathbb{R}\setminus\{0\}$", L"$\mathbb{R}^+$", L"$\mathbb{R}^-$", "Las otras opciones son falsas." )
answer = 1
radioq(choices, answer)
```

c. ¿Cómo es el crecimiento la función logarítmica $\log_a(x)$?

```{julia}
#| hold: true
#| echo: false
using LaTeXStrings
choices = (L"Creciente si $a>1$", L"Creciente si $a<1$", L"Decreciente si $0 < a <1$", L"Creciente si $0 < a < 1$")
answers = [1,3]
multiq(choices, answers)
```

d. ¿Cómo es la concavidad la función logarítmica $\log_a(x)$?

```{julia}
#| hold: true
#| echo: false
using LaTeXStrings
choices = (L"Cóncava hacia arriba si $a>1$", L"Cóncava hacia abajo si $a<1$", L"Cóncava hacia arriba si $0 < a < 1$", L"Cóncava hacia abajo si $0 < a < 1$")
answers = [2,3]
multiq(choices, answers)
```

:::

:::{#exr-funciones-trigonométricas}
¿Cuál es el periodo y la amplitud de la función $2\cos(x/2)$? 

```{julia}
#| hold: true
#| echo: false
choices = ("2π y 1", "π y 2", "2π y 2", "4π y 1", "4π y 2")
answer = 5
radioq(choices, answer)
```
:::


:::{#exr-sucesiones-propuesto-3}
¿Cuál de las gráficas corresponde a la siguiente función paramétrica?

$$
f(t)=
\begin{cases}
\operatorname{sen}(2t)-\cos(t) \\
\operatorname{sen}(t)+\cos(t)
\end{cases}
$$

```{julia}
#| echo: false
using Plots
u0(t)=sin(t)
v0(t)=cos(t)
v1(t) = 2cos(t)
v2(t) = sin(t)+cos(t)
u1(t) = sin(2t)-cos(t)
u2(t) = cos(2t)
p1 = plot(u0, v0, 0, 2pi, aspect_ratio=1.0, legend=false)
p2 = plot(u1, v2, 0, 2pi, aspect_ratio=1.0, legend=false)
p3 = plot(u0, v2, 0, 2pi, aspect_ratio=1.0, legend=false)
p4 = plot(u2, v2, 0, 2pi, aspect_ratio=1.0, legend=false)
l = @layout [a b; c d]
p = plot(p1, p2, p3, p4, layout=l)
imgfile = "img/funciones-elementales/graficas-funciones-parametricas.png"
savefig(p, imgfile)
hotspotq(imgfile, (1/2,1), (1/2,1))
```
:::




